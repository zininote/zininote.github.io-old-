---
layout: post
title: "INDEX,MATCH: 목록에서 고유한 항목 개수를 세고, 추출하여 다시 재나열하는 함수식"
updated: 2021-08-16
tags: [msoffice,formula]
---

## 고유항목 추출 함수식

엑셀에서 중복된 항목을 제거하여 고유한 항목만 표시하고자 할 땐, 보통 "데이터" > "중복된 항목 제거" 기능을 사용한다. 하지만 일회성 기능으로 새로운 Data 목록이 주어진다면 다시 기능을 작동시켜야 한다. 하지만 이를 함수식으로 나타낼 수 있다면, 목록이 바뀔 때마다 자동으로 고유한 항목 리스트도 업데이트가 될 것이다.

아래는 데이터목록에서 고유항목의 개수를 계산하고, 고유항목만을 추출하여 재나열하는 함수식이다.

![그림00](/img/msoffice/formula/formula-0021.png)

```excel
= ROUND( SUMPRODUCT( 1 / COUNTIFS( 데이터목록, 데이터목록 )), 0 )
{= IFERROR( INDEX( 데이터목록, MATCH( 0, COUNTIFS( 이미구한고유항목목록, 데이터목록 ), 0 )), "" )}
```
{:.excel}

위쪽 함수는 고유항목의 개수를, 아래쪽 함수는 고유항목만을 추출하여 나열하는 함수식이다.

`데이터목록`은 고유항목의 개수나 고유항목 추출을 하고자 하는 데이터들을 범위지정한다. `이미구한고유항목목록`은 위 예시에서 E7 셀에 들어가있는 수식의 모양을 보면 무엇인지 감이 온다. 참조 모양을 보면 `$E$6:E6` 라고 되어 있는데, 앞쪽만 절대참조 모양으로 구현되어 있음과, E7 셀 안에 있는 수식임에도 E6 을 참조로 지정하고 있음에 주의하자.

이는 `이미구한고유항목목록`을 손쉽게 만들어내기 위한 트릭이다. 예를들어 E7 셀에 들어있는 수식을 E10 셀로 복사를 해보면 `이미구한공유항목목록` 부분에는 `$E$6:E9`라는 참조범위가 들어가게 된다.

그리고 개수 구하는 수식은 그냥 입력하면 되지만, 고유항목들을 재나열하는 수식은, 수식 앞뒤로 붙어있는 중괄호 {} 를 제외한 수식을 타이핑하고 Ctrl + Shift + Enter 키를 눌러 **배열수식으로 입력**을 해야 정상작동 한다. 수식 입력셀에 포커싱을 두면, 입력한 수식 앞뒤로 중괄호가 보이는데, 배열수식으로 입력했음을 나타내는 표시다.

또한 고유한 항목들이 들어갈 범위를 한번에 선택하고 입력하는 것이 아니라, 셀 하나하나마다 배열수식으로 입력을 해야 한다. 위 그림 예시로 보자면, E7 셀에 수식을 CTRL + SHIFT + ENTER 를 눌러 배열수식으로 입력한 뒤, 그 셀을 복사하여 E8 ~ E19 셀에 붙여넣으면 된다.

주의할 점이 있는데 개수 구하는 식이나 추출식 모두 **`데이터목록`에 Blank 가 있으면 오동작** 할 수 있다는 점이다. COUNTIFS 함수 특성 때문인데 [MS오피스 도움말](https://support.microsoft.com/ko-kr/office/countifs-%ed%95%a8%ec%88%98-dda3dc6e-f74e-4aee-88bc-aa8c2a866842?ui=ko-kr&rs=ko-kr&ad=kr)을 보면 중간 정도에 COUNTIFS 함수는 Blank (빈 셀) 를 0 으로 판단해버린다는 문장을 확인할 수 있을 것이다. 즉 Blank 를 Blank 로 안보기 때문에 문제가 생길 수 있다.

그리고, `이미구한고유항목목록`은 수식 구조상 고유항목이 추출되는 영역의 바로 위 셀부터 참조에 들어간다. (위 예시에서도 고유항목이 추출되는 셀은 E7 부터지만 수식의 범위는 E6 부터이다.) 따라서 고유항목이 추출되는 시작점의 바로 위 셀이 우연히 `데이터목록`에 있는 어떤 항목과 일치한다면 역시 오동작을 할 수도 있다. (위 예시를 만들고, E6 셀의 내용을 "고유항목목록" 에서 "바나나" 로 바꿔보면 무슨 뜻인지 알 수 있다.)

## 함수식 이해

먼저 고유항목 개수를 구하는 함수식이다.

COUNTIFS 함수의 두번째 인수는 원래 특정값과 같은 조건을 주게 되지만, 이처럼 범위를 넣게되면 범위끼리 서로 같은 항목을 포함여부를 조사하여 그 개수를 배열로 반환한다. 먼저 개수를 구하는 함수식을 보면 COUNTIFS 함수가 같은 `데이터목록`끼리 범위 지정을 한 것을 볼 수 있다. 같은 항목 포함여부를 조사하면 당연히 그 항목이 `데이터목록` 안에 포함되어 있는 개수를 반환한다.

위 예시를 가지고 구체적으로 살펴보면, "배"라는 항목의 COUNTIFS 결과는 3 이 되는데 분수 형태를 띄고 있으므로 1/3 이 구해진다. 모든 "배" 에 대해서 1/3 이라는 결과가 SUMPRODUCT 함수로 더해지므로 1/3 + 1/3 + 1/3 이 되어 최종적으로는 1 이 된다. 결과적으로 중복항목이 몇개가 있든 항목마다 1 이라는 결과가 나오므로 그 합계는 고유항목 개수가 되는 원리이다.

제일 바깥에 있는 ROUND 함수는 혹시라도 있을지 모르는 분수식 계산의 오류 방지 차원에서 넣은 것이다. (이 오류에 대해서는 [별도 포스팅](https://tezblog.github.io/post/excel-floating-number-calculation-problem)을 참고하자.)

다음으로, 고유항목만을 골라 재나열하는 함수식이다.

먼저 COUNTIFS 함수는 `이미구한고유항목목록`과 `데이터목록` 일치여부를 비교하고 있다. `데이터목록`에서 새로운 항목이 나온다면 `이미구한고유항목목록`에는 없을 것이므로 개수가 0 이 되고 이를 MATCH 함수가 받아 INDEX 함수로 해당 항목을 출력하는 형태다.

더 이상 고유항목이 없다면 INDEX, MATCH 함수는 에러를 반환하게 되는데, 이 경우에는 제일 바깥에 있는 IFERROR 함수가 빈칸 ("") 으로 처리한다.